import org.apache.ivy.plugins.resolver.URLResolver

buildscript {
    repositories.mavenCentral()
    dependencies.classpath 'org.gradlefx:gradlefx:0.6.3'
}

task wrapper(type: Wrapper) {
    gradleVersion = '1.1'
}

subprojects { subproject ->
    apply plugin: 'gradlefx'
    apply plugin: 'ideafx'
    apply plugin: 'maven'

    repositories {
        add(new URLResolver()) {
            name = 'Apache'
            addArtifactPattern 'http://apache.cu.be/flex/4.9.0/binaries/[module]-[revision]-bin.[ext]'
        }
    }

    dependencies {
        flexSDK 'org.apache:apache-flex-sdk:4.9.0@zip'
    }

    sdkAutoInstall {
        showPrompts = false
    }

    //remove Unit Testing application generated by FlashBuilder
    compileFlex.doFirst {
        delete srcDirs[0] + '/FlexUnitApplication.mxml'
    }

}

gradle.afterProject { project ->
    if (project.metaClass.hasProperty(project, 'type') && project.type.isLib()) {
        project.uploadArchives {
            repositories.mavenDeployer {
                repository url: "file://${System.getProperty('user.home')}/.m2/repository/"

                pom.project {
                    name project.name
                    groupId 'net.riastar.components'
                }
            }
        }
    }
}
